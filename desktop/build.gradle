plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7' apply false
}

def commonProject = project.parent
def jdk8 = System.getProperty("java.version").startsWith("1.8")

mainClassName = 'org.frontend.app.DesktopMain'

distributions {
    main {
        distributionBaseName = commonProject.name + '-desktop'
        contents {
            from "${commonProject.projectDir}/src/main/webapp/"
        }
    }
}
tasks.run {
    jvmArgs "-Dbrowser.rootdir=${commonProject.projectDir}/src/main/webapp/"
}
if (!jdk8) {
    apply plugin: 'org.openjfx.javafxplugin'
    javafx {
        version = "11"
        modules = ["javafx.web"]
    }
}
dependencies {
    implementation commonProject
    implementation "org.netbeans.html:net.java.html.boot:1.7.2"
    implementation "com.dukescript.api:javafx.beaninfo:0.6"
    runtimeOnly "com.dukescript.api:javafx.base:8.60.11"
    runtimeOnly "org.netbeans.html:net.java.html.boot.fx:1.7.2"
}
